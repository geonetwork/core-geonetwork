PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

# Extract list of resources ID that doesn't have a dcat:CatalogRecord.
# It first check for the first resource dct:identifier.
# If none, check on the resource about.
# If none, ignore the resource.
SELECT ?catalogId ?resourceId
WHERE {
  {
    ?resourceId a dcat:Dataset .
    OPTIONAL {
      ?catalogId dcat:dataset ?resourceId .
      ?catalogId a dcat:Catalog .
      ?resourceId dct:identifier ?identifier
    }
    FILTER (NOT EXISTS {
                         ?recordId a dcat:CatalogRecord .
                         ?recordId foaf:primaryTopic ?resourceId .
                       }
    )
    FILTER (bound(?identifier) || !isBlank(?resourceId))

  } UNION {
    ?catalogId a dcat:Catalog .
    ?resourceId a dcat:DataService .
    ?catalogId dcat:service ?resourceId .
    OPTIONAL {
      ?resourceId dct:identifier ?identifier .
    }
    BIND (if (bound(?identifier), ?identifier, ?resourceId) as ?brId)
    FILTER (NOT EXISTS {
                         ?recordId a dcat:CatalogRecord .
                         ?recordId foaf:primaryTopic ?resourceId .
                       }
    )
    FILTER (bound(?identifier) || !isBlank(?resourceId))
  }
}
GROUP BY ?resourceId ?catalogId
